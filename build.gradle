plugins {
    id 'java'
    id 'application'
    id 'com.diffplug.spotless' version '6.22.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.javadiscord.bot'
version = project.version

application {
    mainClass = 'com.javadiscord.bot.Main'
}

shadowJar {
    archiveBaseName.set('Java-Discord-Bot')
    archiveClassifier.set('')
    archiveVersion.set('')
}

spotless {
    java {
        importOrder()
        googleJavaFormat()
                .aosp()
                .reflowLongStrings()
                .formatJavadoc(true)
                .reorderImports(true)
                .groupArtifact('com.google.googlejavaformat:google-java-format')
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.docker-java:docker-java:3.3.6'

    implementation 'com.theokanning.openai-gpt3-java:service:0.18.2'
    implementation 'com.rometools:rome:2.1.0'
    implementation 'net.dv8tion:JDA:5.0.0-beta.18'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.16.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.0'

    implementation 'org.apache.logging.log4j:log4j-api:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.1'
   // annotationProcessor 'org.apache.logging.log4j:log4j-core:2.22.1'
}

tasks.register('stage') {
    dependsOn build
}

test {
    useJUnitPlatform()
}

tasks.register('bumpPatchVersion') {
    doLast {
        def versionParts = project.version.split("\\.")
        def newVersion = "${versionParts[0]}.${versionParts[1]}.${versionParts[2] as Integer + 1}"
        project.version = newVersion
        file('gradle.properties').text = "version=$newVersion"
        println "Version bumped to: $newVersion"
    }
}
tasks.register('bumpMinorVersion') {
    doLast {
        def versionParts = project.version.split("\\.")
        def newVersion = "${versionParts[0]}.${versionParts[1] as Integer + 1}.0"
        project.version = newVersion
        file('gradle.properties').text = "version=$newVersion"
        println "Version bumped to: $newVersion"
    }
}
tasks.register('bumpMajorVersion') {
    doLast {
        def versionParts = project.version.split("\\.")
        def newVersion = "${versionParts[0] as Integer + 1}.0.0"
        project.version = newVersion
        file('gradle.properties').text = "version=$newVersion"
        println "Version bumped to: $newVersion"
    }
}
